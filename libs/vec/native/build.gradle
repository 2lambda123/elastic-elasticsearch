/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the Elastic License
 * 2.0 and the Server Side Public License, v 1; you may not use this file except
 * in compliance with, at your election, the Elastic License 2.0 or the Server
 * Side Public License, v 1.
 */
apply plugin: 'c'

import org.gradle.internal.os.OperatingSystem
OperatingSystem os = OperatingSystem.current();

// This task is deliberately not depended upon. Run it manually if you
// modify the native code,
//   ./gradlew :libs:elasticsearch-vec:native:buildNativeLibrary
// then commit the native library, e.g.
//   git commit libs/vec/platform/darwin-aarch64/libvec.dylib
//
// Look at the disassemble:
//  objdump --disassemble-symbols=_dot8s libs/vec/native/build/libs/vec/shared/libvec.dylib

tasks.register("buildNativeLibrary") {
  dependsOn tasks.named("copyLib")
}

model {
  platforms {
    aarch64 {
      architecture "aarch64"
    }
  }
  components {
    vec(NativeLibrarySpec) {
      targetPlatform "aarch64"
      binaries.withType(SharedLibraryBinarySpec) {
        cCompiler.args "-O3"
        linker.args "-march=native"
        linker.args "-mcpu=native"
        linker.args "-mtune=native"
      }
    }
  }
}

tasks.register("copyLib", Copy).configure {
  description "Copy native lib"
  from "build/libs/vec/shared/${os.getSharedLibraryName('vec')}"
  into "../platform/${os.getNativePrefix()}-aarch64"
  dependsOn tasks.named("vecSharedLibrary")
}
