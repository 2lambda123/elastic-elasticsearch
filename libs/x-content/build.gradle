/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the Elastic License
 * 2.0 and the Server Side Public License, v 1; you may not use this file except
 * in compliance with, at your election, the Elastic License 2.0 or the Server
 * Side Public License, v 1.
 */

import java.util.stream.Collectors

apply plugin: 'elasticsearch.build'
apply plugin: 'elasticsearch.publish'

configurations {
  providerImpl
}

dependencies {
  api project(':libs:elasticsearch-core')

  providerImpl project(':libs:elasticsearch-x-content:impl')

  testImplementation(project(":test:framework")) {
    exclude group: 'org.elasticsearch', module: 'elasticsearch-x-content'
  }
}

tasks.named('forbiddenApisMain').configure {
  // x-content does not depend on server
  // TODO: Need to decide how we want to handle for forbidden signatures with the changes to core
  replaceSignatureFiles 'jdk-signatures'
}

tasks.named("thirdPartyAudit").configure {
  ignoreMissingClasses(
          // from com.fasterxml.jackson.dataformat.yaml.YAMLMapper (jackson-dataformat-yaml)
          'com.fasterxml.jackson.databind.ObjectMapper',
          'com.fasterxml.jackson.databind.cfg.MapperBuilder'
  )
}

tasks.named("dependencyLicenses").configure {
  mapping from: /jackson-.*/, to: 'jackson'
}

tasks.named("jarHell").configure { enabled = false }

def generateProviderJarsList = tasks.register("generateProviderJarsList") {
  File generatedResourcesDir = new File(buildDir, 'generated-resources')
  File jarsFile = new File(generatedResourcesDir, 'org/elasticsearch/xcontent/internal/provider-jars.txt')
  inputs.property('jars', configurations.providerImpl)
  outputs.dir(generatedResourcesDir)
  doLast {
    jarsFile.parentFile.mkdirs()
    jarsFile.setText(configurations.providerImpl.files.stream()
      .map(f -> f.name).collect(Collectors.joining('\n')), 'UTF-8')
  }
}
sourceSets.main.output.dir(generateProviderJarsList)
