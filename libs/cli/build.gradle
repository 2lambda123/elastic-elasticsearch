/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the Elastic License
 * 2.0 and the Server Side Public License, v 1; you may not use this file except
 * in compliance with, at your election, the Elastic License 2.0 or the Server
 * Side Public License, v 1.
 */
apply plugin: 'elasticsearch.java'

dependencies {
  api 'net.sf.jopt-simple:jopt-simple:5.0.2'
  api project(':libs:elasticsearch-core')
}

sourceSets {
  unsupportedJdkVersionEntrypoint
}

tasks.named('compileUnsupportedJdkVersionEntrypointJava').configure {
  targetCompatibility = JavaVersion.VERSION_1_7
}

tasks.named("jar") {
  manifest {
    attributes("Multi-Release": "true")
  }

  from(sourceSets.unsupportedJdkVersionEntrypoint.output)
  eachFile { details ->
    if (details.path.equals("org/elasticsearch/cli/Launcher.class") &&
      sourceSets.main.output.asFileTree.contains(details.file)) {
      details.relativePath = details.relativePath.prepend("META-INF/versions/17")
    }
  }
}
