/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the Elastic License
 * 2.0 and the Server Side Public License, v 1; you may not use this file except
 * in compliance with, at your election, the Elastic License 2.0 or the Server
 * Side Public License, v 1.
 */

import org.elasticsearch.gradle.transform.UnzipTransform

configurations {
  libs {
    attributes.attribute(ArtifactTypeDefinition.ARTIFACT_TYPE_ATTRIBUTE, ArtifactTypeDefinition.DIRECTORY_TYPE)
  }
  runtimePath
}

var zstdVersion = "1.5.5"

dependencies {
  registerTransform(UnzipTransform, transformSpec -> {
    transformSpec.getFrom().attribute(ArtifactTypeDefinition.ARTIFACT_TYPE_ATTRIBUTE, ArtifactTypeDefinition.JAR_TYPE);
    transformSpec.getTo().attribute(ArtifactTypeDefinition.ARTIFACT_TYPE_ATTRIBUTE, ArtifactTypeDefinition.DIRECTORY_TYPE);
  });
  libs "org.elasticsearch:zstd:${zstdVersion}:darwin-aarch64"
  libs "org.elasticsearch:zstd:${zstdVersion}:darwin-x86-64"
  libs "org.elasticsearch:zstd:${zstdVersion}:linux-aarch64"
  libs "org.elasticsearch:zstd:${zstdVersion}:linux-x86-64"
  libs "org.elasticsearch:zstd:${zstdVersion}:windows-x86-64"
}

def extractLibs = tasks.register('extractLibs', Copy)
extractLibs.configure {
  from configurations.libs
  into layout.buildDirectory.dir('platform')
  // TODO: fix architecture to use underscore instead of dash
  rename '([^-]+-)x86-64(.*)', '$1x86_64$2'
}

artifacts {
  runtimePath extractLibs
}
