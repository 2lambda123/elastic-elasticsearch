
buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
    }
}

apply plugin: 'elasticsearch.build'
apply plugin: 'com.github.johnrengelman.shadow'

description = 'JDBC driver for Elasticsearch'

forbiddenApisMain {
    // does not depend on core, so only jdk and http signatures should be checked
    signaturesURLs = [this.class.getResource('/forbidden/jdk-signatures.txt')]
}

dependencies {
    compile (xpackProject('plugin:sql:sql-shared-client')) {
        transitive = false
    }
    compile (xpackProject('plugin:sql:sql-shared-proto')) {
        transitive = false
    }
    compile (project(':libs:x-content')) {
        transitive = false
    }
    compile project(':libs:elasticsearch-core')
    runtime "com.fasterxml.jackson.core:jackson-core:${versions.jackson}"
    testCompile "org.elasticsearch.test:framework:${version}"
}

dependencyLicenses {
    mapping from: /sql-shared-proto.*/, to: 'elasticsearch'
    mapping from: /sql-shared-client.*/, to: 'elasticsearch'
    mapping from: /jackson-.*/, to: 'jackson'
    mapping from: /elasticsearch-core.*/, to: 'elasticsearch'
    ignoreSha 'sql-shared-proto'
    ignoreSha 'sql-shared-client'
    ignoreSha 'elasticsearch'
}

shadowJar {
    baseName = 'elasticsearch-jdbc'
    classifier = null
    relocate 'com.fasterxml', 'org.elasticsearch.fasterxml'
}

assemble.dependsOn shadowJar

// Use the jar for testing so the tests are more "real"
test {
    classpath -= compileJava.outputs.files
    classpath -= configurations.compile
    classpath -= configurations.runtime
    classpath += shadowJar.outputs.files
    dependsOn shadowJar
}
