
/*
 * This project contains XContent protocol classes shared between server and http client
 */
apply plugin: 'elasticsearch.build'

description = 'Request and response objects shared by the cli, jdbc ' +
        'and the Elasticsearch plugin'

// we want to keep the JDKs in our IDEs set to JDK 8 until minimum JDK is bumped to 9 so we do not include this source set in our IDEs
if (!isEclipse && !isIdea) {
  sourceSets {
    java9 {
      java {
        srcDirs = ['src/main/java9']
      }
    }
  }
  
  configurations {
    java9Compile.extendsFrom(compile)
  }
  
  dependencies {
    java9Compile sourceSets.main.output
  }

  compileJava9Java {
    sourceCompatibility = 9
    targetCompatibility = 9
  }

  forbiddenApisJava9 {
    if (project.runtimeJavaVersion < JavaVersion.VERSION_1_9) {
      targetCompatibility = JavaVersion.VERSION_1_9.getMajorVersion()
    }
  }

  jar {
    metaInf {
      into 'versions/9'
      from sourceSets.java9.output
    }
    manifest.attributes('Multi-Release': 'true')
  }
}


dependencies {
    compile (project(':libs:core')) {
        transitive = false
    }
    compile (project(':libs:x-content')) {
        transitive = false
    }
    compile "joda-time:joda-time:${versions.joda}"
    runtime "com.fasterxml.jackson.core:jackson-core:${versions.jackson}"
    
    if (!isEclipse && !isIdea) {
      java9Compile sourceSets.main.output
    }


    testCompile "org.elasticsearch.test:framework:${version}"
}

forbiddenApisMain {
    //sql does not depend on server, so only jdk signatures should be checked
    replaceSignatureFiles 'jdk-signatures'
}

dependencyLicenses {
    mapping from: /elasticsearch-core.*/, to: 'elasticsearch'
    mapping from: /jackson-.*/, to: 'jackson'
    ignoreSha 'elasticsearch-core'
}
