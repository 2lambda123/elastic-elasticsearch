/*
 * The minimal dependencies REST-based SQL client that is used by CLI and JDBC
 */

apply plugin: 'elasticsearch.build'

description = 'Code shared between jdbc and cli'

dependencies {
  api xpackProject('plugin:sql:sql-proto')
  api "com.fasterxml.jackson.core:jackson-core:${versions.jackson}"
  testImplementation project(":test:framework")
  testImplementation(project(xpackModule('core'))) {
    capabilities {
      requireCapability("org.elasticsearch.gradle:core-test-artifacts")
    }
  }
}

tasks.named("dependencyLicenses").configure {
  mapping from: /jackson-.*/, to: 'jackson'
}

tasks.named('forbiddenApisMain').configure {
  // does not depend on core, so only jdk and http signatures should be checked
  replaceSignatureFiles 'jdk-signatures'
}

tasks.named('forbiddenApisTest').configure {
  bundledSignatures -= 'jdk-non-portable'
  bundledSignatures += 'jdk-internal'
}

tasks.named("forbiddenPatterns").configure {
  exclude '**/*.keystore'
}
