import org.elasticsearch.gradle.info.BuildParams
import java.nio.file.Files

// Tell the tests we're running with ssl enabled
integTest.runner {
  systemProperty 'tests.ssl.enabled', 'true'
}

configurations {
  nodeKeystores
  clientKeyStores
}

dependencies {
  testRuntime(project(path: ":x-pack:plugin:sql:qa:ssl-utils", configuration: 'certificateJars'))
  nodeKeystores(project(path: ":x-pack:plugin:sql:qa:ssl-utils", configuration: 'nodeKeyStores'))
  clientKeyStores(project(path: ":x-pack:plugin:sql:qa:ssl-utils", configuration: 'clientKeyStores'))
}

forbiddenPatterns {
  exclude '**/*.cert'
}

integTest.runner {
  onlyIf {
    // Do not attempt to form a cluster in a FIPS JVM, as doing so with a JKS keystore will fail.
    // TODO Revisit this when SQL CLI client can handle key/certificate instead of only Keystores.
    // https://github.com/elastic/elasticsearch/issues/32306
    BuildParams.inFipsJvm == false
  }
}


// location of generated keystores and certificates
File keystoreDir = new File(project.buildDir, 'keystore')

// Generate the node's keystore
File nodeKeystore = file("$keystoreDir/test-node.jks")

// Generate the client's keystore
File clientKeyStore = file("$keystoreDir/test-client.jks")

tasks.register("resolveKeyStores") {
  inputs.files(configurations.nodeKeystores)
  inputs.files(configurations.clientKeyStores)
  doLast {
    Files.copy(configurations.nodeKeystores.files.singleFile.toPath(), nodeKeystore.toPath())
    Files.copy(configurations.clientKeyStores.files.singleFile.toPath(), clientKeyStore.toPath())
  }
}

tasks.withType(org.elasticsearch.gradle.testclusters.TestClustersAware).configureEach {
  dependsOn "resolveKeyStores"
}

testClusters.integTest {
  // The setup that we actually want
  setting 'xpack.license.self_generated.type', 'trial'
  setting 'xpack.security.http.ssl.enabled', 'true'
  setting 'xpack.security.transport.ssl.enabled', 'true'

  // ceremony to set up ssl
  setting 'xpack.security.transport.ssl.keystore.path', 'test-node.jks'
  setting 'xpack.security.http.ssl.keystore.path', 'test-node.jks'
  keystore 'xpack.security.transport.ssl.keystore.secure_password', 'keypass'
  keystore 'xpack.security.http.ssl.keystore.secure_password', 'keypass'


  // copy keystores into config/
  extraConfigFile nodeKeystore.name, nodeKeystore
  extraConfigFile clientKeyStore.name, clientKeyStore
}
