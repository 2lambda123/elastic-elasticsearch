import org.elasticsearch.gradle.info.BuildParams
import java.nio.file.Files

// Tell the tests we're running with ssl enabled
integTest.runner {
  systemProperty 'tests.ssl.enabled', 'true'
}

configurations {
  nodeKeystores
  clientKeyStores
}

dependencies {
  testRuntime(project(path: ":x-pack:plugin:sql:qa:ssl-utils", configuration: 'certificateJars'))
  nodeKeystores(project(path: ":x-pack:plugin:sql:qa:ssl-utils", configuration: 'nodeKeyStores'))
  clientKeyStores(project(path: ":x-pack:plugin:sql:qa:ssl-utils", configuration: 'clientKeyStores'))
}

// location of generated keystores and certificates
File keystoreDir = new File(project.buildDir, 'keystore')

// Generate the node's keystore
File nodeKeystore = file("$keystoreDir/test-node.jks")

// Generate the client's keystore
File clientKeyStore = file("$keystoreDir/test-client.jks")

tasks.register("resolveKeyStores") {
  inputs.files(configurations.nodeKeystores)
  inputs.files(configurations.clientKeyStores)
  outputs.dir(keystoreDir)
  doLast {
    Files.copy(configurations.nodeKeystores.singleFile.toPath(), nodeKeystore.toPath())
    Files.copy(configurations.clientKeyStores.singleFile.toPath(), clientKeyStore.toPath())
  }
}

tasks.withType(org.elasticsearch.gradle.testclusters.TestClustersAware).configureEach {
  dependsOn "resolveKeyStores"
}

testClusters.integTest {
  // The setup that we actually want
  setting 'xpack.license.self_generated.type', 'trial'
  setting 'xpack.security.http.ssl.enabled', 'true'
  setting 'xpack.security.transport.ssl.enabled', 'true'

  // ceremony to set up ssl
  setting 'xpack.security.transport.ssl.keystore.path', 'test-node.jks'
  setting 'xpack.security.http.ssl.keystore.path', 'test-node.jks'
  keystore 'xpack.security.transport.ssl.keystore.secure_password', 'keypass'
  keystore 'xpack.security.http.ssl.keystore.secure_password', 'keypass'


  // copy keystores into config/
  extraConfigFile nodeKeystore.name, nodeKeystore
  extraConfigFile clientKeyStore.name, clientKeyStore
}
