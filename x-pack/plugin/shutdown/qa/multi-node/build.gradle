import org.elasticsearch.gradle.VersionProperties

apply plugin: 'elasticsearch.java-rest-test'

dependencies {
  javaRestTestImplementation(testArtifact(project(xpackModule('core'))))
}

def clusterCredentials = [
        username: providers.systemProperty('tests.rest.cluster.username')
                .forUseAtConfigurationTime()
                .getOrElse('test_admin'),
        password: providers.systemProperty('tests.rest.cluster.password')
                .forUseAtConfigurationTime()
                .getOrElse('x-pack-test-password')
]

tasks.named("javaRestTest").configure {
  systemProperty 'tests.rest.cluster.username', clusterCredentials.username
  systemProperty 'tests.rest.cluster.password', clusterCredentials.password
}

testClusters.all {
  testDistribution = 'DEFAULT'
  numberOfNodes = 4

  setting 'xpack.security.enabled', 'true'
  user username: clusterCredentials.username, password: clusterCredentials.password, role: 'superuser'
}
