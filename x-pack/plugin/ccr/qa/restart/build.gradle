import org.elasticsearch.gradle.test.RestIntegTestTask

apply plugin: 'elasticsearch.testclusters'
apply plugin: 'elasticsearch.standalone-test'

dependencies {
    testCompile project(':x-pack:plugin:ccr:qa')
}

task 'leader-cluster'(type: RestIntegTestTask) {
    mustRunAfter(precommit)
    runner {
        systemProperty 'tests.target_cluster', 'leader'
    }
}
testClusters.'leader-cluster' {
    distribution = "DEFAULT"
    setting 'xpack.license.self_generated.type', 'trial'
}

task 'follow-cluster'(type: RestIntegTestTask) {
    dependsOn 'leader-cluster'
    useCluster testClusters.'leader-cluster'
    runner {
        systemProperty 'tests.target_cluster', 'follow'
        nonInputProperties.systemProperty 'tests.leader_host',
                "${-> testClusters.'leader-cluster'.getAllHttpSocketURI().get(0)}"
    }
}
testClusters.'follow-cluster' {
    distribution = "DEFAULT"
    setting 'xpack.monitoring.collection.enabled', 'true'
    setting 'xpack.license.self_generated.type', 'trial'
    setting 'cluster.remote.leader_cluster.seeds',
            { "\"${ testClusters.'leader-cluster'.getAllTransportPortURI().join(",")}\"" }
}

// TODO create a test with cluster task
task followClusterRestartTest(type: Test) {
    dependsOn 'leader-cluster', 'follow-cluster'
    useCluster testClusters.'leader-cluster'
    useCluster testClusters.'follow-cluster'

    maxParallelForks = 1
    systemProperty 'tests.rest.load_packaged', 'false'
    systemProperty 'tests.target_cluster', 'follow-restart'
    def nonInputProperties = new CommandLineArgumentProvider() {
        private final Map<String, Object> systemProperties = [:]

        void systemProperty(String key, Object value) {
            systemProperties.put(key, value)
        }

        @Override
        Iterable<String> asArguments() {
            return systemProperties.collect { key, value ->
                "-D${key}=${value.toString()}".toString()
            }
        }
    }
    doFirst {
        testClusters.'follow-cluster'.restart()

        nonInputProperties.systemProperty 'tests.leader_host',
                "${-> testClusters.'leader-cluster'.getAllHttpSocketURI().get(0)}"
        nonInputProperties.systemProperty 'tests.rest.cluster',
                "${-> testClusters.'follow-cluster'.getAllHttpSocketURI().join(",")}"
    }
    jvmArgumentProviders.add(nonInputProperties)
}

check.dependsOn followClusterRestartTest
test.enabled = false
