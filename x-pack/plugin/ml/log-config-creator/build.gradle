import org.elasticsearch.gradle.precommit.PrecommitTasks

apply plugin: 'elasticsearch.build'
/* We don't use the 'application' plugin because it builds a zip and tgz which
 * we don't want. */

archivesBaseName = 'log-config-creator'

description = 'Create log file import configurations'

dependencies {
    compile "org.elasticsearch:elasticsearch-cli:${version}"
    compile project(':libs:core')
    compile project(':libs:grok')
    compile project(':libs:x-content')
    compile 'com.ibm.icu:icu4j:61.1'
    compile 'joda-time:joda-time:2.10'
    compile 'net.sf.supercsv:super-csv:2.4.0'

    testCompile "org.elasticsearch.test:framework:${version}"
}

dependencyLicenses.enabled = false

/*
 * Bundle all dependencies into the main jar and mark it as executable it
 * can be easilly shipped around and used.
 */
jar {
    from({
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
        configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
    }) {
        // We don't need the META-INF from the things we bundle. For now.
        exclude 'META-INF/*'
    }
    manifest {
        attributes 'Main-Class': 'org.elasticsearch.xpack.ml.configcreator.LogConfigCreator'
    }
}

forbiddenApisMain {
    // log-config-creator does not depend on server
    // TODO: Need to decide how we want to handle for forbidden signatures with the changes to server
    signaturesURLs = [PrecommitTasks.getResource('/forbidden/jdk-signatures.txt')]
}

