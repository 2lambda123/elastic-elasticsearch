import org.elasticsearch.gradle.info.BuildParams

apply plugin: 'elasticsearch.esplugin'
apply plugin: 'elasticsearch.internal-cluster-test'

esplugin {
  name 'x-pack-ilm'
  description 'Elasticsearch Expanded Pack Plugin - Index Lifecycle Management'
  classname 'org.elasticsearch.xpack.ilm.IndexLifecycle'
  extendedPlugins = ['x-pack-core']
  hasNativeController false
  requiresKeystore true
}
archivesBaseName = 'x-pack-ilm'

dependencies {
  compileOnly project(path: xpackModule('core'), configuration: 'default')
  testImplementation project(path: xpackModule('core'), configuration: 'testArtifacts')
  testImplementation project(path: xpackModule('data-streams'), configuration: 'default')
}

// add all sub-projects of the qa sub-project
gradle.projectsEvaluated {
  project.subprojects
    .find { it.path == project.path + ":qa" }
    .subprojects
    .findAll { it.path.startsWith(project.path + ":qa") }
    .each { check.dependsOn it.check }
}

// TODO(talevy): should this even be here? TBD
test {
  if (BuildParams.isSnapshotBuild() == false) {
    systemProperty 'es.rollup_v2_feature_enabled', 'true'
  }
}
