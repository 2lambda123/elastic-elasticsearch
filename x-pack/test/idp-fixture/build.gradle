apply plugin: 'elasticsearch.build'
apply plugin: 'elasticsearch.test.fixtures'

test.enabled = false

preProcessFixture.doLast {
  // We need to create these up-front because if docker creates them they will be owned by root and we won't be
  // able to clean them up
  file("${buildDir}/shared/keytabs/${it}").mkdirs()
  file("${buildDir}/shared/krb-conf/${it}").mkdirs()
  new File("${buildDir}/shared/krb-conf/ldap-krb5.conf").text = ""
  new File("${buildDir}/shared/keytabs/ldap.keytab").text = ""
}

postProcessFixture {
    inputs.dir("${buildDir}/shared")
    File confTemplate = file("${buildDir}/shared/krb-conf/krb5.conf.template")
    File confFile = file("${buildDir}/shared/krb-conf/krb5.conf")
    File ldapKeytab = file("${buildDir}/shared/keytabs/ldap.keytab")
    outputs.file(confFile)
    doLast {
        println "krb5.conf.template exists ? " + confTemplate.exists()
        println "ldap.keytab exists ? " + ldapKeytab.exists()
        assert confTemplate.exists()
        String confContents = confTemplate.text
            .replace("\${MAPPED_PORT}", "${ext."test.fixtures.kdc-kadmin.tcp.88"}")
        confFile.text = confContents
        println "krb5.conf exists after writing ? " + confFile.exists()
    }
}