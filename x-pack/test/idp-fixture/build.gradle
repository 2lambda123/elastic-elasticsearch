import org.elasticsearch.gradle.Architecture
import org.elasticsearch.gradle.internal.docker.DockerBuildTask
import org.elasticsearch.gradle.internal.info.BuildParams

apply plugin: 'elasticsearch.java'
apply plugin: 'elasticsearch.cache-test-fixtures'
apply plugin: 'elasticsearch.deploy-test-fixtures'

dockerFixtures {
  idp {
    dockerContext = file("src/main/resources/idp")
    version = "1.0"
    baseImages = ["openjdk:11.0.16-jre"]
  }
  openldap {
    dockerContext = file("src/main/resources/openldap")
    version = "1.0"
    baseImages = ["osixia/openldap:1.4.0"]
  }
}

dependencies {
  testImplementation project(':test:framework')

  api project(':test:fixtures:testcontainer-utils')
  api "junit:junit:${versions.junit}"
}

tasks.withType(DockerBuildTask).configureEach {
  noCache = BuildParams.isCi()
  push = BuildParams.isCi()
  getPlatforms().addAll( Architecture.values().collect{ it.dockerPlatform } )
}
