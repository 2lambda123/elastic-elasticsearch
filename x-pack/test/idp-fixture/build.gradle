import org.elasticsearch.gradle.VersionProperties
import org.elasticsearch.gradle.Architecture
import static org.elasticsearch.gradle.internal.distribution.InternalElasticsearchDistributionTypes.DOCKER;

apply plugin: 'elasticsearch.test.fixtures'
apply plugin: 'elasticsearch.java'

description = 'Fixture for MinIO Storage service'

configurations.all {
  transitive = false
}

dependencies {
  testImplementation project(':test:framework')
  api "junit:junit:${versions.junit}"
  api "org.testcontainers:testcontainers:${versions.testcontainer}"
  implementation "com.carrotsearch.randomizedtesting:randomizedtesting-runner:${versions.randomizedrunner}"
  implementation "org.slf4j:slf4j-api:${versions.slf4j}"
  implementation "com.github.docker-java:docker-java-api:${versions.docker}"
  runtimeOnly "com.github.docker-java:docker-java-transport-zerodep:${versions.docker}"
  runtimeOnly "com.github.docker-java:docker-java-transport:${versions.docker}"
  runtimeOnly "com.github.docker-java:docker-java-core:${versions.docker}"
  runtimeOnly "org.apache.commons:commons-compress:${versions.commonsCompress}"
  runtimeOnly "org.rnorth.duct-tape:duct-tape:${versions.ductTape}"
  runtimeOnly "org.rnorth.duct-tape:duct-tape:${versions.ductTape}"
}

dockerCompose {
  composeAdditionalArgs = ['--compatibility']
}

tasks.named("preProcessFixture").configure {
  file("${testFixturesDir}/shared/oidc").mkdirs()
}

tasks.register("copyFiles", Sync) {
  from file("oidc/override.properties.template")
  into "${buildDir}/config"
  doLast {
    file("${buildDir}/config").setReadable(true, false)
    file("${buildDir}/config/override.properties.template").setReadable(true, false)
  }
}

tasks.named("postProcessFixture").configure {
  dependsOn "copyFiles"
  inputs.dir("${testFixturesDir}/shared/oidc")
  File confTemplate = file("${buildDir}/config/override.properties.template")
  File confFile = file("${testFixturesDir}/shared/oidc/override.properties")
  outputs.file(confFile)
  doLast {
    assert confTemplate.exists()
    String confContents = confTemplate.text
      .replace("\${MAPPED_PORT}", "${ext."test.fixtures.oidc-provider.tcp.8080"}")
    confFile.text = confContents
  }
}

tasks.named('composePull').configure {
  enabled = false // this task fails due to docker-compose oddities
}
