import org.elasticsearch.gradle.internal.info.BuildParams
import org.elasticsearch.gradle.internal.test.RestIntegTestTask

apply plugin: 'elasticsearch.internal-testclusters'
apply plugin: 'elasticsearch.standalone-rest-test'
apply plugin: 'elasticsearch.rest-resources'

dependencies {
  testImplementation project(':x-pack:qa')
}

restResources {
  restApi {
    include '_common', 'bulk', 'field_caps', 'security', 'search', 'clear_scroll', 'scroll', 'async_search', 'cluster',
      'indices', 'open_point_in_time', 'close_point_in_time', 'terms_enum'
  }
}

tasks.register('fulfilling-cluster', RestIntegTestTask) {
  mustRunAfter("precommit")
  systemProperty 'tests.rest.suite', 'fulfilling_cluster'
}

// randomise between sniff and proxy modes
boolean proxyMode = (new Random(Long.parseUnsignedLong(BuildParams.testSeed.tokenize(':').get(0), 16))).nextBoolean()

def fulfillingCluster = testClusters.register('fulfilling-cluster') {

  setting 'xpack.security.enabled', 'true'
  module ':modules:analysis-common'
  module ':modules:lang-painless'
  module ':modules:data-streams'

  module ':x-pack:plugin:mapper-constant-keyword'
  module ':x-pack:plugin:async-search'

 // setting 'xpack.license.self_generated.type', 'trial'
  user username: "test_user", password: "x-pack-test-password"
}

testClusters.register('querying-cluster') {

  setting 'xpack.security.enabled', 'true'
  module ':modules:analysis-common'
  module ':modules:lang-painless'
  module ':modules:data-streams'
  module ':x-pack:plugin:mapper-constant-keyword'
  module ':x-pack:plugin:async-search'
 // setting 'xpack.license.self_generated.type', 'trial'

  if (proxyMode) {
    setting 'cluster.remote.my_remote_cluster.mode', 'proxy'
    setting 'cluster.remote.my_remote_cluster.proxy_address', {
      "\"${fulfillingCluster.get().getAllTransportPortURI().get(0)}\""
    }
  } else {
    setting 'cluster.remote.my_remote_cluster.seeds', {
      fulfillingCluster.get().getAllTransportPortURI().collect { "\"$it\"" }.toString()
    }
  }
  setting 'cluster.remote.connections_per_cluster', "1"

  user username: "test_user", password: "x-pack-test-password"
}

tasks.register('querying-cluster', RestIntegTestTask) {
  dependsOn 'fulfilling-cluster'
  useCluster fulfillingCluster
  systemProperty 'tests.rest.suite', 'querying_cluster'
  if (proxyMode) {
    systemProperty 'tests.rest.blacklist', [
      'local_cluster/10_basic/Add persistent remote cluster based on the preset cluster',
      'local_cluster/20_info/Add persistent remote cluster based on the preset cluster and check remote info',
      'local_cluster/20_info/Fetch remote cluster info for existing cluster',
      'local_cluster/70_connection_mode_configuration/*',
    ].join(",")
  }
}

tasks.register("integTest") {
  dependsOn 'querying-cluster'
}

tasks.named("check").configure { dependsOn("integTest") }
