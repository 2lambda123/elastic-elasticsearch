import org.elasticsearch.gradle.internal.BwcVersions
import org.elasticsearch.gradle.internal.info.BuildParams
import org.elasticsearch.gradle.testclusters.StandaloneRestIntegTestTask
import org.elasticsearch.gradle.util.GradleUtils

apply plugin: 'elasticsearch.internal-java-rest-test'
apply plugin: 'elasticsearch.internal-yaml-rest-test'
apply plugin: 'elasticsearch.bwc-test'

dependencies {
  implementation project(':test:framework')
  implementation project(':test:test-clusters')
  implementation project(':x-pack:qa')
  javaRestTestImplementation testArtifact(project(xpackModule('core')))
  yamlRestTestImplementation testArtifact(project(xpackModule('core')))
  javaRestTestImplementation project(':client:rest-high-level')
}

GradleUtils.extendSourceSet(project, "main", "yamlRestTest", tasks.named("yamlRestTest"))
GradleUtils.extendSourceSet(project, "main", "javaRestTest", tasks.named("javaRestTest"))

restResources {
  restApi {
    include '*'
  }
}

tasks.named("yamlRestTest") { enabled = false}

BuildParams.bwcVersions.withWireCompatible { bwcVersion, baseName ->
  def javaRestTest = tasks.register("v${bwcVersion}#javaRestTest", StandaloneRestIntegTestTask) {
    usesBwcDistribution(bwcVersion)
    systemProperty('tests.upgrade_from_version', bwcVersion)
    systemProperty('tests.ml.skip', Boolean.toString(BwcVersions.isMlCompatible(bwcVersion) == false))
  }

  def yamlRestTest = tasks.register("v${bwcVersion}#yamlRestTest", StandaloneRestIntegTestTask) {
    testClassesDirs = sourceSets.yamlRestTest.output.classesDirs
    classpath = sourceSets.yamlRestTest.runtimeClasspath
    usesBwcDistribution(bwcVersion)
    systemProperty('tests.upgrade_from_version', bwcVersion)
    // Disable ML tests for incompatible systems
    if (BwcVersions.isMlCompatible(bwcVersion) == false) {
      systemProperty 'tests.rest.blacklist', ['*/30_ml_jobs_crud/*', '*/40_ml_datafeed_crud/*', '*/90_ml_data_frame_analytics_crud'].join(',')
    }
  }

  tasks.register(bwcTaskName(bwcVersion)) {
    dependsOn(javaRestTest, yamlRestTest)
  }
}
