apply plugin: 'elasticsearch.standalone-test'
apply plugin: 'elasticsearch.test.fixtures'

dependencies {
  testImplementation project(path: xpackModule('core'))
  testImplementation(project(xpackModule('security'))) {
    capabilities {
      requireCapability("org.elasticsearch.gradle:security-test-artifacts")
    }
  }
  testImplementation(project(xpackModule('core'))) {
    capabilities {
      requireCapability("org.elasticsearch.gradle:core-test-artifacts")
    }
  }
}

testFixtures.useFixture ":x-pack:test:idp-fixture", "openldap"

Project idpFixtureProject = xpackProject("test:idp-fixture")
String outputDir = "${project.buildDir}/generated-resources/${project.name}"
def copyIdpTrust = tasks.register("copyIdpTrust", Copy) {
  from idpFixtureProject.file('openldap/certs/ca.jks');
  from idpFixtureProject.file('openldap/certs/ca_server.pem');
  into outputDir
}
project.sourceSets.test.output.dir(outputDir, builtBy: copyIdpTrust)
