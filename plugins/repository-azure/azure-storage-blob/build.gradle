import org.elasticsearch.gradle.JavaClassPublicifier;

apply plugin: 'elasticsearch.java'
apply plugin: 'com.github.johnrengelman.shadow'

configurations {
  originalJar {
    transitive = false
  }
}

dependencies {
  originalJar "com.azure:azure-storage-blob:${project.parent.versions.azure}"
  implementation "com.azure:azure-storage-blob:${project.parent.versions.azure}"
}

String blobsServiceClass = 'com/azure/storage/blob/implementation/BlobsImpl$BlobsService.class'
String containersServiceClass = 'com/azure/storage/blob/implementation/ContainersImpl$ContainersService.class'
String blockBlobsServiceClass = 'com/azure/storage/blob/implementation/BlockBlobsImpl$BlockBlobsService.class'
String servicesServiceClass = 'com/azure/storage/blob/implementation/ServicesImpl$ServicesService.class'

tasks.create('extractBlobsServiceClass', Copy).configure {
  from({ zipTree(configurations.originalJar.singleFile) }) {
    include blobsServiceClass
    include containersServiceClass
    include blockBlobsServiceClass
    include servicesServiceClass
  }
  into project.file('build/original')
}

tasks.create('makeBlobsServicePublic', JavaClassPublicifier).configure {
  classFiles = [blobsServiceClass, containersServiceClass, blockBlobsServiceClass, servicesServiceClass]
  inputDir = project.layout.buildDirectory.dir('original')
  outputDir = project.layout.buildDirectory.dir('modified')
}

sourceSets.main.java.compiledBy(tasks.named('makeBlobsServicePublic')) { myTask -> myTask.outputDir }
