import org.elasticsearch.gradle.internal.info.BuildParams
// Subprojects aren't published so do not assemble
subprojects {
  project.tasks.matching { it.name.equals('assemble') }.configureEach {
    enabled = false
  }
  if (BuildParams.inFipsJvm) {
    project.tasks.configureEach {
      enabled = false
    }
  }
  
  // configure project dependencies for yaml rest test plugin.
  // plugin defaults to external available artifacts
  // 
  project.getPluginManager().withPlugin("elasticsearch.yaml-rest-test", new Action<AppliedPlugin>() {
    @Override
    void execute(AppliedPlugin appliedPlugin) {
        project.dependencies {
          yamlRestTestImplementation project(":test:framework")
          restTestSpecs project(":rest-api-spec")
        }
    }
  })
}

configure(project('painless-whitelist')) {
  configurations.all {
    resolutionStrategy.dependencySubstitution {
      substitute module('org.elasticsearch.plugin:elasticsearch-scripting-painless-spi') with project(':modules:lang-painless:spi')
      substitute module('org.elasticsearch.test:logger-usage') with project(':test:logger-usage')
    }
  }
}

configure(project('security-authorization-engine')) {
  configurations.all {
    resolutionStrategy.dependencySubstitution {
      substitute module('org.elasticsearch.plugin:x-pack-core') with project(':x-pack:plugin:core')
      substitute module('org.elasticsearch.client:elasticsearch-rest-high-level-client') with project(':client:rest-high-level')
      substitute module('org.elasticsearch.test:logger-usage') with project(':test:logger-usage')
    }
  }
}
