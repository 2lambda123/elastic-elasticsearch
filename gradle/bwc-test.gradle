import org.elasticsearch.gradle.Architecture
import org.elasticsearch.gradle.Version
import org.elasticsearch.gradle.info.BuildParams

ext.bwcTaskName = { Version version ->
  return "v${version}#bwcTest"
}

def bwcTestSnapshots = tasks.register("bwcTestSnapshots") {
  if (project.bwc_tests_enabled) {
    dependsOn tasks.matching { task -> BuildParams.bwcVersions.unreleased.any { version -> bwcTaskName(version) == task.name } }
  }
}

tasks.register("bwcTest") {
  description = 'Runs backwards compatibility tests.'
  group = 'verification'

  if (project.bwc_tests_enabled) {
    dependsOn tasks.matching { it.name ==~ /v[0-9\.]+#bwcTest/ }
  }
}

tasks.withType(Test).configureEach {
  onlyIf { project.bwc_tests_enabled }
  nonInputProperties.systemProperty 'tests.bwc', 'true'
}

tasks.named("check").configure {
  dependsOn(bwcTestSnapshots)
}

tasks.matching { it.name.equals("test") }.configureEach { enabled = false }

// Since the 7.12 branch has no backward compatible version on ARM don't complain about tests that won't execute
// TODO: We can remove this as soon as 7.12.0 is released.
tasks.named("testingConventions").configure { onlyIf { Architecture.current() == Architecture.X64 } }
