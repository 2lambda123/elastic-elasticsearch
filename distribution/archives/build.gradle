/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the Elastic License
 * 2.0 and the Server Side Public License, v 1; you may not use this file except
 * in compliance with, at your election, the Elastic License 2.0 or the Server
 * Side Public License, v 1.
 */

import java.nio.file.Files
import java.nio.file.Path

apply plugin: 'elasticsearch.internal-distribution-archive-setup'

CopySpec archiveFiles(String platform, String distributionType, String os, String architecture) {
  boolean isTestDistro = platform == null;
  return copySpec {
    into("elasticsearch-${version}") {
      into('lib') {
        with libFiles(platform)
      }
      into('config') {
        dirMode 0750
        fileMode 0660
        with configFiles(distributionType, isTestDistro)
        from {
          dirMode 0750
          jvmOptionsDir.getParent()
        }
      }
      into('bin') {
        with binFiles(distributionType, isTestDistro)
      }
      into("darwin".equals(os) ? 'jdk.app' : 'jdk') {
        if (isTestDistro == false) {
          with jdkFiles(project, os, architecture)
        }
      }
      into('') {
        from {
          dirMode 0755
          logsDir.getParent()
        }
      }
      into('') {
        from {
          dirMode 0755
          pluginsDir.getParent()
        }
      }
      from(rootProject.projectDir) {
        include 'README.asciidoc'
      }
      from(rootProject.file('licenses')) {
        include isTestDistro ? 'SSPL-1.0+ELASTIC-LICENSE-2.0.txt' : 'ELASTIC-LICENSE-2.0.txt'
        rename { 'LICENSE.txt' }
      }

      with noticeFile(isTestDistro)
      into('modules') {
        if (isTestDistro) {
          integTestModulesFiles
        } else {
          with modulesFiles(platform)
        }
      }
    }
  }
}

distribution_archives {
  integTestZip {
    content {
      archiveFiles(null, 'zip', null, 'x64')
    }
  }

  windowsZip {
    archiveClassifier = 'windows-x86_64'
    content {
      archiveFiles('windows-x86_64', 'zip', 'windows', 'x64')
    }
  }

  darwinTar {
    archiveClassifier = 'darwin-x86_64'
    content {
      archiveFiles('darwin-x86_64', 'tar', 'darwin', 'x64')
    }
  }

  darwinAarch64Tar {
    archiveClassifier = 'darwin-aarch64'
    content {
      archiveFiles('darwin-aarch64', 'tar', 'darwin', 'aarch64')
    }
  }

  linuxAarch64Tar {
    archiveClassifier = 'linux-aarch64'
    content {
      archiveFiles('linux-aarch64', 'tar', 'linux', 'aarch64')
    }
  }

  linuxTar {
    archiveClassifier = 'linux-x86_64'
    content {
      archiveFiles('linux-x86_64', 'tar', 'linux', 'x64')
    }
  }
}

subprojects {
  apply plugin: 'distribution'
  apply plugin: 'elasticsearch.internal-distribution-archive-check'

  group = "org.elasticsearch.distribution.default"
}
