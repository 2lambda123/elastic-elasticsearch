import org.elasticsearch.gradle.Architecture
import org.elasticsearch.gradle.ElasticsearchDistribution.Flavor
import org.elasticsearch.gradle.Variant
import org.elasticsearch.gradle.LoggedExec
import org.elasticsearch.gradle.VersionProperties
import org.elasticsearch.gradle.docker.DockerBuildTask
import org.elasticsearch.gradle.info.BuildParams
import org.elasticsearch.gradle.testfixtures.TestFixturesPlugin

import java.nio.file.Path
import java.security.MessageDigest
import java.util.stream.Collectors

apply plugin: 'elasticsearch.standalone-rest-test'
apply plugin: 'elasticsearch.test.fixtures'
apply plugin: 'elasticsearch.internal-distribution-download'
apply plugin: 'elasticsearch.rest-resources'

testFixtures.useFixture()

configurations {
  dockerPlugins
  aarch64DockerSource
  dockerSource
  aarch64OssDockerSource
  ossDockerSource
}

dependencies {
  aarch64DockerSource project(path: ":distribution:archives:linux-aarch64-tar")
  dockerSource project(path: ":distribution:archives:linux-tar")
  aarch64OssDockerSource project(path: ":distribution:archives:oss-linux-aarch64-tar")
  ossDockerSource project(path: ":distribution:archives:oss-linux-tar")
}

ext.expansions = { Architecture architecture, Variant variant, boolean local ->
  String classifier
  if (local || variant == Variant.UBI) {
    if (architecture == Architecture.AARCH64) {
      classifier = "linux-aarch64"
    } else if (architecture == Architecture.X64) {
      classifier = "linux-x86_64"
    } else {
      throw new IllegalArgumentException("Unsupported architecture [" + architecture + "]")
    }
  } else {
    /* When sourcing the Elasticsearch build remotely, the same Dockerfile needs
     * to be able to fetch the artifact for any supported platform. We can't make
     * the decision here. Bash will interpolate the `arch` command for us. */
    classifier = "linux-\$(arch)"
  }

  final String elasticsearch = "elasticsearch-${variant == Variant.OSS ? 'oss-' : ''}${VersionProperties.elasticsearch}-${classifier}.tar.gz"

  String buildArgs = '#'
  if (variant == Variant.UBI) {
    buildArgs = """
ARG BASE_REGISTRY=nexus-docker-secure.levelup-nexus.svc.cluster.local:18082
ARG BASE_IMAGE=redhat/ubi/ubi8
ARG BASE_TAG=8.2
ARG TARBALL=${elasticsearch}
"""
  }

  String baseImage = 'centos:8'
  if (variant == Variant.UBI) {
    baseImage = '${BASE_REGISTRY}/${BASE_IMAGE}:${BASE_TAG} AS builder'
  } else if (variant == Variant.UBI_MINIMAL) {
    baseImage = 'registry.access.redhat.com/ubi8/ubi-minimal:8.2'
  }

  /* Both the following Dockerfile commands put the resulting artifact at
   * the same location, regardless of classifier, so that the commands that
   * follow in the Dockerfile don't have to know about the runtime
   * architecture. */
  String sourceElasticsearch
  if (local) {
    sourceElasticsearch = "COPY ${elasticsearch} /opt/elasticsearch.tar.gz"
  } else if (variant == Variant.UBI) {
    sourceElasticsearch = 'COPY ${TARBALL} /opt/elasticsearch.tar.gz'
  } else {
    sourceElasticsearch = """
RUN curl --retry 8 -S -L \\
      --output /opt/elasticsearch.tar.gz \\
      https://artifacts.elastic.co/downloads/elasticsearch/$elasticsearch
"""
  }

  return [
    'base_image'          : baseImage,
    'bin_dir'             : variant == Variant.UBI ? 'scripts' : 'bin',
    'build_args'          : buildArgs,
    'build_date'          : BuildParams.buildDate,
    'config_dir'          : variant == Variant.UBI ? 'scripts' : 'config',
    'git_revision'        : BuildParams.gitRevision,
    'license'             : variant == Variant.OSS ? 'Apache-2.0' : 'Elastic-License',
    'package_manager'     : variant == Variant.UBI_MINIMAL ? 'microdnf' : 'yum',
    'source_elasticsearch': sourceElasticsearch,
    'variant'             : variant.name().toLowerCase(),
    'version'             : VersionProperties.elasticsearch,
    'version_hash'        : variant == Variant.UBI ? getChecksum(configurations.dockerSource.singleFile) : ''
  ]
}

private static String getChecksum(File file) {
  MessageDigest digest = MessageDigest.getInstance("SHA-512")

  FileInputStream fis = new FileInputStream(file)
  byte[] byteArray = new byte[1024]
  int bytesCount = 0

  //Read file data and update in message digest
  while ((bytesCount = fis.read(byteArray)) != -1) {
    digest.update(byteArray, 0, bytesCount)
  }

  fis.close()

  //Get the hash's bytes
  byte[] bytes = digest.digest()

  //This bytes[] has bytes in decimal format;
  //Convert it to hexadecimal format
  StringBuilder sb = new StringBuilder()
  for (int i = 0; i < bytes.length; i++) {
    sb.append(Integer.toString((bytes[i] & 0xff) + 0x100, 16).substring(1))
  }

  return sb.toString()
}

private static String buildPath(Architecture architecture, Variant variant) {
  String variantString = variant == Variant.DEFAULT ? '' : variant.name().toLowerCase().replace('_', '-') + '-'
  return "build/${architecture == Architecture.AARCH64 ? 'aarch64-' : ''}${variantString}docker"
}

private static String taskName(String prefix, Architecture architecture, Variant variant, String suffix) {
  String variantString = variant == Variant.DEFAULT ? '' : variant.name().tokenize("_")*.toLowerCase()*.capitalize().join()
  return "${prefix}${architecture == Architecture.AARCH64 ? 'Aarch64' : ''}${variantString}${suffix}"
}

project.ext {
  dockerBuildContext = { Architecture architecture, Variant variant, boolean local ->
    copySpec {
      final Map<String,String> varExpansions = expansions(architecture, variant, local)
      final Path projectDir = project.projectDir.toPath()

      if (variant == Variant.UBI) {
        into('scripts') {
          from projectDir.resolve("src/docker/bin")
          from(projectDir.resolve("src/docker/config")) {
            exclude '**/oss'
          }
        }
        from(projectDir.resolve("src/docker/ubi")) {
          expand(varExpansions)
        }
      } else {
        into('bin') {
          from projectDir.resolve("src/docker/bin")
        }

        into('config') {
          /*
         * Oss and default distribution can have different configuration, therefore we want to allow overriding the default configuration
         * by creating config files in oss or default build-context sub-modules.
         */
          duplicatesStrategy = DuplicatesStrategy.INCLUDE
          from projectDir.resolve("src/docker/config")
          if (variant == Variant.OSS) {
            from projectDir.resolve("src/docker/config/oss")
          }
        }
      }

      from(project.projectDir.toPath().resolve("src/docker/Dockerfile")) {
        expand(varExpansions)
      }
    }
  }
}

void addCopyDockerContextTask(Architecture architecture, Variant variant) {
  tasks.register(taskName("copy", architecture, variant, "DockerContext"), Sync) {
    expansions(architecture, variant, true).findAll { it.key != 'build_date' }.each { k, v ->
      inputs.property(k, { v.toString() })
    }
    into buildPath(architecture, variant)

    with dockerBuildContext(architecture, variant, true)

    if (architecture == Architecture.AARCH64) {
      if (variant == Variant.OSS) {
        from configurations.aarch64OssDockerSource
      } else {
        from configurations.aarch64DockerSource
      }
    } else {
      if (variant == Variant.OSS) {
        from configurations.ossDockerSource
      } else {
        from configurations.dockerSource
      }
    }

    from configurations.dockerPlugins
  }
}

def createAndSetWritable(Object... locations) {
  locations.each { location ->
    File file = file(location)
    file.mkdirs()
    file.setWritable(true, false)
  }
}

tasks.register("copyKeystore", Sync) {
  from project(':x-pack:plugin:core')
    .file('src/test/resources/org/elasticsearch/xpack/security/transport/ssl/certs/simple/testnode.jks')
  into "${buildDir}/certs"
  doLast {
    file("${buildDir}/certs").setReadable(true, false)
    file("${buildDir}/certs/testnode.jks").setReadable(true, false)
  }
}

elasticsearch_distributions {
  Architecture.values().each { eachArchitecture ->
    Flavor.values().each { distroFlavor ->
      "docker_$distroFlavor${ eachArchitecture == Architecture.AARCH64 ? '_aarch64' : '' }" {
        architecture = eachArchitecture
        flavor = distroFlavor
        type = 'docker'
        version = VersionProperties.getElasticsearch()
        failIfUnavailable = false // This ensures we don't attempt to build images if docker is unavailable
      }
    }
  }
}

tasks.named("preProcessFixture").configure {
  dependsOn elasticsearch_distributions.docker_default, elasticsearch_distributions.docker_oss
  dependsOn "copyKeystore"
  doLast {
    // tests expect to have an empty repo
    project.delete(
      "${buildDir}/repo",
      "${buildDir}/oss-repo"
    )
    createAndSetWritable(
      "${buildDir}/repo",
      "${buildDir}/oss-repo",
      "${buildDir}/logs/default-1",
      "${buildDir}/logs/default-2",
      "${buildDir}/logs/oss-1",
      "${buildDir}/logs/oss-2"
    )
  }
}

tasks.named("processTestResources").configure {
  from project(':x-pack:plugin:core')
    .file('src/test/resources/org/elasticsearch/xpack/security/transport/ssl/certs/simple/testnode.jks')
}

tasks.register("integTest", Test) {
  outputs.doNotCacheIf('Build cache is disabled for Docker tests') { true }
  maxParallelForks = '1'
  include '**/*IT.class'
}

tasks.named("check").configure {
  dependsOn "integTest"
}

void addBuildDockerImage(Architecture architecture, Variant variant) {
  final TaskProvider<DockerBuildTask> buildDockerImageTask =
          tasks.register(taskName("build", architecture, variant, "DockerImage"), DockerBuildTask) {
    onlyIf { Architecture.current() == architecture }
    TaskProvider<Sync> copyContextTask = tasks.named(taskName("copy", architecture, variant, "DockerContext"))
    dependsOn(copyContextTask)
    dockerContext.fileProvider(copyContextTask.map { it.destinationDir })

    if (variant == Variant.OSS) {
      tags = [
        "docker.elastic.co/elasticsearch/elasticsearch-oss:${VersionProperties.elasticsearch}",
        "elasticsearch-oss:test"
      ]
    } else {
      String suffix = variant == Variant.UBI ? '-ubi8' : variant == Variant.UBI_MINIMAL ? '-ubi8-minimal' : ''
      tags = [
        "elasticsearch${suffix}:${VersionProperties.elasticsearch}",
        "docker.elastic.co/elasticsearch/elasticsearch${suffix}:${VersionProperties.elasticsearch}",
        "docker.elastic.co/elasticsearch/elasticsearch-full${suffix}:${VersionProperties.elasticsearch}",
        "elasticsearch${suffix}:test",
      ]
    }
  }
  tasks.named("assemble").configure {
    dependsOn(buildDockerImageTask)
  }
}

for (final Architecture architecture : Architecture.values()) {
  for (Variant variant : Variant.values()) {
    if (architecture != Architecture.X64 && variant == Variant.UBI) {
      continue
    }
    addCopyDockerContextTask(architecture, variant)
    addBuildDockerImage(architecture, variant)
  }
}

// We build the images used in compose locally, but the pull command insists on using a repository
// thus we must disable it to prevent it from doing so.
// Everything will still be pulled since we will build the local images on a pull
if (tasks.findByName("composePull")) {
  tasks.composePull.enabled = false
}

/*
 * The export subprojects write out the generated Docker images to disk, so
 * that they can be easily reloaded, for example into a VM for distribution testing
 */
subprojects { Project subProject ->
  if (subProject.name.endsWith('-export')) {
    apply plugin: 'distribution'

    final Architecture architecture = subProject.name.contains('aarch64-') ? Architecture.AARCH64 : Architecture.X64
    final boolean oss = subProject.name.contains('oss-')
    // We don't export UBI at the moment
    final Variant variant = oss ? Variant.OSS : Variant.DEFAULT

    final String arch = architecture == Architecture.AARCH64 ? '-aarch64' : ''
    final String suffix = oss ? '-oss' : ''

    def exportTaskName = taskName("export", architecture, variant, "DockerImage")
    def buildTaskName = taskName("build", architecture, variant, "DockerImage")
    def tarFile = "${parent.projectDir}/build/elasticsearch${arch}${suffix}_test.${VersionProperties.elasticsearch}.docker.tar"

    final TaskProvider<LoggedExec> exportDockerImageTask = tasks.register(exportTaskName, LoggedExec) {
      inputs.file("${parent.projectDir}/build/markers/${buildTaskName}.marker")
      executable 'docker'
      outputs.file(tarFile)
      args "save",
        "-o",
        tarFile,
        "elasticsearch${suffix}:test"

      dependsOn(parent.path + ":" + buildTaskName)
      onlyIf { Architecture.current().name().toLowerCase().equals(architecture) }
    }

    artifacts.add('default', file(tarFile)) {
      type 'tar'
      name "elasticsearch${arch}${suffix}"
      builtBy exportTaskName
    }

    tasks.named("assemble").configure {
      dependsOn(exportTaskName)
    }
  }
}
