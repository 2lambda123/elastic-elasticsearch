import org.elasticsearch.gradle.LoggedExec
import org.gradle.internal.jvm.Jvm

apply plugin: 'elasticsearch.build'

repositories {
  mavenCentral()
}

dependencies {
  testImplementation "junit:junit:${versions.junit}"
  testImplementation "org.hamcrest:hamcrest:${versions.hamcrest}"
}

// This tests depend on ES core
disableTasks('forbiddenApisMain', 'forbiddenApisTest')

tasks.named('testingConventions').configure {
  naming.clear()
  naming {
    Tests {
      baseClass 'junit.framework.TestCase'
    }
  }
}

String inputFile = "${parent.parent.buildDir}/outputs/default/log4j2.properties"
String outputFile = "${project.buildDir}/distributions/log4j2.properties"
String transformSource = "${project.projectDir}/src/main/java/org/elasticsearch/transform/log4j/TransformLog4jConfig.java"

  tasks.register("transformDistributionConfig", LoggedExec) {
  dependsOn project(':distribution').tasks.named('buildDefaultLog4jConfig')
  inputs.files(inputFile, transformSource)
  outputs.file(outputFile)

  executable Jvm.current().getJavaExecutable()
  args transformSource, inputFile, outputFile
}

configurations.register('log4jConfig')

artifacts.add('log4jConfig', file(outputFile)) {
  type 'file'
  name 'log4j2.properties'
  builtBy 'transformDistributionConfig'
}
