/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

apply plugin: 'elasticsearch.standalone-rest-test'
apply plugin: 'elasticsearch.rest-test'

integTestCluster {
  // Whitelist reindexing from the local node so we can test it.
  setting 'reindex.remote.whitelist', '127.0.0.1:*'

}

configurations {
  oldesFixture
  es2
  es1
  es090
}

for (String version : ['2', '1', '090']) {
  Task unzip = task("unzipEs${version}", type: Sync) {
    Configuration oldEsDependency = configurations['es' + version]
    dependsOn oldEsDependency
    from {
      oldEsDependency.collect { zipTree(it) }
    }
    into temporaryDir
  }
  Task fixture = task("oldEs${version}Fixture",
        type: org.elasticsearch.gradle.test.AntFixture) {
    dependsOn project.configurations.oldesFixture
    dependsOn unzip
    executable = new File(project.javaHome, 'bin/java')
    env 'CLASSPATH', "${ -> project.configurations.oldesFixture.asPath }"
    args 'oldes.OldElasticsearch',
         baseDir,
         unzip.temporaryDir,
         version == '090'
  }
  integTestCluster.dependsOn fixture
  integTestRunner {
    systemProperty "es${version}.port", "${ -> fixture.addressAndPort }"
  }
}

dependencies {
  oldesFixture project(':test:fixtures:old-elasticsearch')
  es2 'org.elasticsearch.distribution.zip:elasticsearch:2.4.5@zip'
  es1 'org.elasticsearch:elasticsearch:1.7.6@zip'
  es090 'org.elasticsearch:elasticsearch:0.90.13@zip'
}
