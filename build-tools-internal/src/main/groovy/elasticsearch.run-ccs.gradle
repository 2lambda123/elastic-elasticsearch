/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the Elastic License
 * 2.0 and the Server Side Public License, v 1; you may not use this file except
 * in compliance with, at your election, the Elastic License 2.0 or the Server
 * Side Public License, v 1.
 */

import org.elasticsearch.gradle.testclusters.RunTask

boolean proxyMode = Boolean.valueOf(providers.systemProperty('proxyMode').getOrElse('true'))

def fulfillingCluster = testClusters.register('fulfilling-cluster') {
  testDistribution = providers.systemProperty('run.distribution').orElse('default').get()

  setting 'xpack.security.enabled', 'true'
  setting 'xpack.watcher.enabled', 'false'
  setting 'xpack.ml.enabled', 'false'
  setting 'xpack.license.self_generated.type', 'trial'
  setting 'remote_cluster.enabled', 'true'
  // TODO
  setting 'remote_cluster.port', '9901'

  user username: 'elastic-admin', password: 'elastic-password', role: '_es_test_root'
}

def fulfillingCluster2 = testClusters.register('fulfilling-cluster2') {
  testDistribution = providers.systemProperty('run.distribution').orElse('default').get()

  setting 'xpack.security.enabled', 'true'
  setting 'xpack.watcher.enabled', 'false'
  setting 'xpack.ml.enabled', 'false'
  setting 'xpack.license.self_generated.type', 'trial'
  setting 'remote_cluster.enabled', 'true'
  // TODO
  setting 'remote_cluster.port', '9902'

  user username: 'elastic-admin', password: 'elastic-password', role: '_es_test_root'
}

def queryingCluster = testClusters.register('querying-cluster') {
  testDistribution = providers.systemProperty('run.distribution').orElse('default').get()

  setting 'xpack.security.enabled', 'true'
  setting 'xpack.watcher.enabled', 'false'
  setting 'xpack.ml.enabled', 'false'
  setting 'xpack.license.self_generated.type', 'trial'
  setting 'cluster.remote.connections_per_cluster', "1"

  user username: 'elastic-admin', password: 'elastic-password', role: '_es_test_root'
  user username: 'ccs-admin', password: 'elastic-password', role: '_ccs_admin'
}

tasks.register("run-ccs", RunTask) {
  useCluster fulfillingCluster2
  useCluster fulfillingCluster
  useCluster queryingCluster
  doFirst {
    queryingCluster.get().getNodes().each { node ->
      if (proxyMode) {
        node.setting('cluster.remote.my_remote_cluster.mode', 'proxy')
        node.setting('cluster.remote.my_remote_cluster.proxy_address', "\"${fulfillingCluster.get().getAllRemoteAccessPortURI().get(0)}\"")
        node.setting('cluster.remote.my_remote_cluster2.mode', 'proxy')
        node.setting('cluster.remote.my_remote_cluster2.proxy_address', "\"${fulfillingCluster2.get().getAllRemoteAccessPortURI().get(0)}\"")
      } else {
        node.setting('cluster.remote.my_remote_cluster.seeds', fulfillingCluster.get().getAllRemoteAccessPortURI().collect { "\"$it\"" }.toString())
        node.setting('cluster.remote.my_remote_cluster2.seeds', fulfillingCluster2.get().getAllRemoteAccessPortURI().collect { "\"$it\"" }.toString())
      }
    }
    queryingCluster.get().restart() //updates the on-disk config

    println "** Querying cluster HTTP endpoints are: ${-> queryingCluster.get().allHttpSocketURI.join(",")}"
    println "** Querying cluster transport endpoints are: ${-> queryingCluster.get().getAllTransportPortURI().join(",")}"

    println "** Fulfilling cluster HTTP endpoints are: ${-> fulfillingCluster.get().allHttpSocketURI.join(",")}"
    println "** Fulfilling cluster transport endpoints are: ${-> fulfillingCluster.get().getAllTransportPortURI().join(",")}"
    println "** Fulfilling cluster remote access endpoints are: ${-> fulfillingCluster.get().getAllRemoteAccessPortURI().join(",")}"

    println "** Fulfilling cluster 2 HTTP endpoints are: ${-> fulfillingCluster2.get().allHttpSocketURI.join(",")}"
    println "** Fulfilling cluster 2 transport endpoints are: ${-> fulfillingCluster2.get().getAllTransportPortURI().join(",")}"
    println "** Fulfilling cluster 2 remote access endpoints are: ${-> fulfillingCluster2.get().getAllRemoteAccessPortURI().join(",")}"
  }
}
