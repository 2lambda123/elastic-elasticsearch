import org.apache.tools.ant.taskdefs.condition.Os
import org.elasticsearch.gradle.Version
import org.elasticsearch.gradle.VersionProperties
import org.elasticsearch.gradle.internal.BwcVersions
import org.elasticsearch.gradle.internal.JarApiComparisonTask
import org.elasticsearch.gradle.internal.info.BuildParams

import static org.elasticsearch.gradle.internal.InternalDistributionBwcSetupPlugin.buildBwcTaskName

configurations {
  stableApi
}

BuildParams.bwcVersions.allIndexCompatible.findAll({ it.onOrAfter(Version.fromString(ext.stableApiSince)) && it != VersionProperties.
  elasticsearchVersion
}).
  each { bwcVersion ->
  def baseName = "v${bwcVersion}"

  BwcVersions.UnreleasedVersionInfo unreleasedVersion = BuildParams.bwcVersions.unreleasedInfo(bwcVersion)
  Configuration stableApiConfiguration = configurations.create("stableApi${baseName}")
  Object libDependency = null

  if (unreleasedVersion) {
    // For unreleased snapshot versions, build them from source
    libDependency = files(project(unreleasedVersion.gradleProjectPath).tasks.named(buildBwcTaskName(project.name)))
  } else {
    // For released versions, download it
    libDependency = "org.elasticsearch:" + project.name + ":${bwcVersion}"
  }

  dependencies {
    "stableApi${baseName}"(libDependency)
  }

  def jarApiComparisonTask = tasks.register(bwcTaskName(bwcVersion), JarApiComparisonTask) {
    oldJar = configurations."stableApi${baseName}"
    newJar = tasks.named("jar").flatMap(t -> t.archiveFile)
  }

  jarApiComparisonTask.configure {
    onlyIf {
      !Os.isFamily(Os.FAMILY_WINDOWS)
    }
  }
}
