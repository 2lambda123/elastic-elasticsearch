import org.elasticsearch.gradle.test.RestTestPlugin

/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */
apply plugin: 'elasticsearch.build'

group = 'org.elasticsearch.client'

dependencies {
  compile "org.elasticsearch:elasticsearch:${version}"
  compile "org.elasticsearch.client:rest:${version}"
  testCompile "org.elasticsearch.test:framework:${version}"
  testCompile "com.carrotsearch.randomizedtesting:randomizedtesting-runner:${versions.randomizedrunner}"
  testCompile "junit:junit:${versions.junit}"
  testCompile "org.hamcrest:hamcrest-all:${versions.hamcrest}"
}

task checkNoOtherDependencies {
  description 'This task fails if any other dependency than core and rest client is added'
  group 'Verification'
  doLast {
    Collection<Dependency> others = configurations.compile.allDependencies - configurations.compile.allDependencies.findAll {
      // Should only contain core and rest client!
      (it.group == project.group && it.name == 'rest') || (it.group == 'org.elasticsearch' && it.name == 'elasticsearch')
    }
    if (others.isEmpty() == false) {
      throw new GradleException(project.path + " must depend only on core and rest client, but found: " + others)
    }
  }
}

compileJava {
  dependsOn checkNoOtherDependencies
}

dependencyLicenses {
  // Don't check licenses for dependency that are part of the elasticsearch project
  // But any other dependency should have its license/notice/sha1
  dependencies = project.configurations.runtime.fileCollection {
    it.group.startsWith('org.elasticsearch') == false
  }
}

RestTestPlugin.createIntegTestTask(project)
