/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

import org.elasticsearch.gradle.test.RestIntegTestTask
import org.elasticsearch.gradle.BuildPlugin
import org.elasticsearch.gradle.precommit.PrecommitTasks

apply plugin: 'elasticsearch.build'
apply plugin: 'nebula.maven-base-publish'
apply plugin: 'nebula.maven-scm'

group = 'org.elasticsearch.client'

dependencies {
    compile "org.elasticsearch:elasticsearch:${version}"
    compile "org.elasticsearch.client:rest:${version}"
    compile "org.apache.httpcomponents:httpcore:${versions.httpcore}"
    compile "org.apache.httpcomponents:httpasyncclient:${versions.httpasync}"
    compile "org.apache.httpcomponents:httpcore-nio:${versions.httpcore}"
    compile "commons-codec:commons-codec:${versions.commonscodec}"
    compile "commons-logging:commons-logging:${versions.commonslogging}"
    compile "com.fasterxml.jackson.core:jackson-core:${versions.jackson}"
    compile "com.fasterxml.jackson.jr:jackson-jr-objects:${versions.jackson}"

    testCompile "org.elasticsearch.test:framework:${version}"
    testCompile "com.carrotsearch.randomizedtesting:randomizedtesting-runner:${versions.randomizedrunner}"
    testCompile "junit:junit:${versions.junit}"
    testCompile "org.hamcrest:hamcrest-all:${versions.hamcrest}"
}

dependencyLicenses {
    // Don't check the client's license. We know it.
    dependencies = project.configurations.runtime.fileCollection {
        it.group.startsWith('org.elasticsearch') == false
    } - project.configurations.provided
    mapping from: /http.*/, to: 'httpclient'
    mapping from: /commons-.*/, to: 'commons'
    mapping from: /jackson-.*/, to: 'jackson'
}

//JarHell is part of es core, which we don't want to pull in
jarHell.enabled=false

task integTest(type: RestIntegTestTask,
                  group: JavaBasePlugin.VERIFICATION_GROUP,
                  description: 'Multi-node tests',
                 dependsOn: test.dependsOn) {
     configure(BuildPlugin.commonTestConfig(project))
     classpath = project.test.classpath
     testClassesDir = project.test.testClassesDir
     include '**/*IT.class'
}

check.dependsOn integTest
integTest.mustRunAfter test

thirdPartyAudit.excludes = [
        //commons-logging optional dependencies
        'org.apache.avalon.framework.logger.Logger',
        'org.apache.log.Hierarchy',
        'org.apache.log.Logger',
        //commons-logging provided dependencies
        'javax.servlet.ServletContextEvent',
        'javax.servlet.ServletContextListener'
]
