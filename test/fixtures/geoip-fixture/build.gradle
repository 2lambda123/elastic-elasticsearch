/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the Elastic License
 * 2.0 and the Server Side Public License, v 1; you may not use this file except
 * in compliance with, at your election, the Elastic License 2.0 or the Server
 * Side Public License, v 1.
 */
apply plugin: 'elasticsearch.java'
apply plugin: 'elasticsearch.test.fixtures'

description = 'Fixture for GeoIPv2 service'
tasks.named("test").configure { enabled = false }

configurations {
  providerImpls
}

dependencies {
  api project(':server')
  api project(':distribution:tools:geoip-cli')
  api project(":libs:elasticsearch-cli")
  api project(":libs:elasticsearch-x-content")
  providerImpls project(path: ":libs:elasticsearch-x-content:impl", configuration: "providerDir")
}

tasks.named("preProcessFixture").configure {
  dependsOn "jar", configurations.runtimeClasspath, configurations.providerImpls
  doLast {
    file("${testFixturesDir}/shared/providers").mkdirs()
    project.copy {
      from jar
      from configurations.runtimeClasspath
      into "${testFixturesDir}/shared"
    }
    project.copy {
      from configurations.providerImpls
      into "${testFixturesDir}/shared/providers"
    }
  }
}
