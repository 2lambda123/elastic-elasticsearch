import org.gradle.api.services.internal.BuildServiceProvider
import org.gradle.api.services.internal.BuildServiceRegistryInternal

/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the Elastic License
 * 2.0 and the Server Side Public License, v 1; you may not use this file except
 * in compliance with, at your election, the Elastic License 2.0 or the Server
 * Side Public License, v 1.
 */
apply plugin: 'elasticsearch.test.fixtures'
apply plugin: 'elasticsearch.java'
apply plugin: 'elasticsearch.cache-test-fixtures'
apply plugin: 'elasticsearch.deploy-test-fixtures'

dockerFixtures {
  krb5dc {
    dockerContext = projectDir
    version = "1.0"
    baseImages = ["ubuntu:14.04"]
  }
}

List<String> services = ["peppa", "hdfs"]

tasks.named("preProcessFixture").configure {
  doLast {
    // We need to create these up-front because if docker creates them they will be owned by root and we won't be
    // able to clean them up
    services.each { fixturesDir.dir("shared/${it}").get().getAsFile().mkdirs() }
  }
}

tasks.named("postProcessFixture").configure { task ->
  inputs.dir(fixturesDir.dir('shared').get().getAsFile())
  services.each { service ->
    File confTemplate = fixturesDir.file("shared/${service}/krb5.conf.template").get().asFile
    File confFile = fixturesDir.file("shared/${service}/krb5.conf").get().asFile
    outputs.file(confFile)
    doLast {
      assert confTemplate.exists()
      String confContents = confTemplate.text
        .replace("\${MAPPED_PORT}", "${ext."test.fixtures.${service}.udp.88"}")
      confFile.text = confContents
    }
  }
}

project.ext.krb5Conf = { s -> file("$testFixturesDir/shared/${s}/krb5.conf") }
project.ext.krb5Keytabs = { s, fileName -> file("$testFixturesDir/shared/${s}/keytabs/${fileName}") }

configurations {
  krb5ConfHdfsFile {
    canBeConsumed = true
    canBeResolved = false
  }
  krb5KeytabsHdfsDir {
    canBeConsumed = true
    canBeResolved = false
  }
}

configurations.all {
  transitive = false
}

dependencies {
  testImplementation project(':test:framework')

  api "junit:junit:${versions.junit}"
  api project(':test:fixtures:testcontainer-utils')
  api "org.testcontainers:testcontainers:${versions.testcontainer}"
  implementation "com.carrotsearch.randomizedtesting:randomizedtesting-runner:${versions.randomizedrunner}"
  implementation "org.slf4j:slf4j-api:${versions.slf4j}"
  implementation "com.github.docker-java:docker-java-api:${versions.dockerJava}"
  implementation "com.fasterxml.jackson.core:jackson-annotations:${versions.jackson}"

  runtimeOnly "com.github.docker-java:docker-java-transport-zerodep:${versions.dockerJava}"
  runtimeOnly "com.github.docker-java:docker-java-transport:${versions.dockerJava}"
  runtimeOnly "com.github.docker-java:docker-java-core:${versions.dockerJava}"
  runtimeOnly "org.apache.commons:commons-compress:${versions.commonsCompress}"
  runtimeOnly "org.rnorth.duct-tape:duct-tape:${versions.ductTape}"

  // ensure we have proper logging during when used in tests
  runtimeOnly "org.slf4j:slf4j-simple:${versions.slf4j}"
  runtimeOnly "org.hamcrest:hamcrest:${versions.hamcrest}"
}


artifacts {
  krb5ConfHdfsFile(krb5Conf('hdfs')) {
    builtBy("postProcessFixture")
  }
  krb5KeytabsHdfsDir(file("$testFixturesDir/shared/hdfs/keytabs/")) {
    builtBy("postProcessFixture")
  }
}
