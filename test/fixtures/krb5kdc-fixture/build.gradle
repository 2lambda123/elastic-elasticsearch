/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

apply plugin: 'elasticsearch.build'

def vagrantEnvVars = [
        'VAGRANT_CWD'           : "${project.projectDir.absolutePath}",
        'VAGRANT_VAGRANTFILE'   : 'Vagrantfile',
        'VAGRANT_PROJECT_DIR'   : "${project.projectDir.absolutePath}"
]

def box = "krb5kdc"

def defaultPrincipals = [ "elasticsearch", "hdfs/hdfs.build.elastic.co" ]

task update(type: org.elasticsearch.gradle.vagrant.VagrantCommandTask) {
    boxName box
    environmentVars vagrantEnvVars
    args 'box', 'update', box
}

task up(type: org.elasticsearch.gradle.vagrant.VagrantCommandTask) {
    boxName = box
    environmentVars = vagrantEnvVars
    args = [ "up", box, '--provision', '--provider', 'virtualbox' ]
    dependsOn update
}

task addDefaultPrincipals {
    dependsOn up
}

for (String principal : defaultPrincipals) {
    def addTask = project.tasks.create("addPrincipal#${principal}", Exec) {
        environment vagrantEnvVars
        commandLine 'vagrant', 'ssh', box, '--command', "sudo bash /elasticsearch/src/main/resources/provision/addprinc.sh $principal"
        dependsOn up
    }
    addDefaultPrincipals.dependsOn(addTask)
}

task halt(type: org.elasticsearch.gradle.vagrant.VagrantCommandTask) {
    boxName = box
    environmentVars = vagrantEnvVars
    args = [ "halt", box ]
}

task destroy(type: org.elasticsearch.gradle.vagrant.VagrantCommandTask) {
    boxName = box
    environmentVars = vagrantEnvVars
    args = [ "destroy", box, "-f"]
    dependsOn halt
}
clean.dependsOn destroy

thirdPartyAudit.enabled = false